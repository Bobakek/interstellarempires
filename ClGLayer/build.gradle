// import org.apache.tools.ant.filters.*

sourceCompatibility = 1.8

sourceSets.main.java.srcDirs = [ "src/" ]
sourceSets.test.java.srcDirs = [ "test/" ]

sourceSets.main.resources.srcDirs = ['assets']
// assetsDir = [ "assets/" ]

repositories {
  mavenLocal()
  mavenCentral()

  maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
  maven { url "https://oss.sonatype.org/content/repositories/releases/" }
}

/** Information about the application and dependencies */
ext {
  appName = 'Interstellar Empires'
  gdxVersion = '1.9.3'
  superUIVersion = '1.0-rc1'
  box2DLightsVersion = '1.4'
  ashleyVersion = '1.7.0'
  aiVersion = '1.7.0'

  version = '0.4a'
}

project.ext.mainClassName = "io.lonelyrobot.empires.cl.gl.Launcher"
project.ext.assetsDir = new File("assets");

/** DEPENDENCIES SECTION */
dependencies {

  /* Include client network layer */
  implementation project(':ClNetworking')

  /* Include shared game framework */
  implementation project(':GameFramework')
  
  implementation group: 'log4j', name: 'log4j', version: '1.2.17'
  
  implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
  implementation "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
  implementation "com.badlogicgames.box2dlights:box2dlights:$box2DLightsVersion"
  implementation "com.badlogicgames.gdx:gdx-ai:$aiVersion"
  implementation "com.badlogicgames.gdx:gdx-controllers:$gdxVersion"

  implementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"

  implementation "com.kotcrab.vis:vis-ui:0.9.4"
  implementation "com.kotcrab.vis:vis-runtime:0.3.0"
  
  // ensure Vector2D (commons-math3) is available to the client
  implementation group: 'org.apache.commons', name: 'commons-math3', version: '3.0'

  implementation "com.underwaterapps.overlap2druntime:overlap2d-runtime-libgdx:0.1.0"
  implementation "net.dermetfan.libgdx-utils:libgdx-utils:0.13.2"
  implementation "net.dermetfan.libgdx-utils:libgdx-utils-box2d:0.13.2"

  implementation "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
  implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
  implementation "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
  implementation "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
  implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
  implementation "com.badlogicgames.gdx:gdx-controllers-desktop:$gdxVersion"
}

/** Packaging task */
jar {
  exclude "META-INF/*.SF"
  exclude "META-INF/*.DSA"
  exclude "META-INF/*.RSA"
  manifest.attributes "Main-Class": project.mainClassName
  dependsOn classes

  // avoid duplicate META-INF files from dependencies
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE

  from sourceSets.main.output
  // include runtime dependencies into fat jar
  from {
    configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
  }
  // exclude common duplicate license/notice files
  exclude 'META-INF/LICENSE', 'META-INF/LICENSE.txt', 'META-INF/NOTICE', 'META-INF/NOTICE.txt'

  from files(project.assetsDir)
}
